      SUBROUTINE ZPE 
      IMPLICIT DOUBLE PRECISION(A-H,O-Z) 
       INCLUDE 'SIZES.i'
       INCLUDE 'FFILES.i'                                               GDH1095
C     -THE HESSIAN MATRIX ISSUED FROM 'LTRD' IN INTERNAL COORDINATES 
C      IS BACK TRANSFORMED IN CARTESIAN COORDINATES, WITHOUT 
C      TAKING INTO ACCOUNT THE RESIDUAL VALUES OF THE GRADIENT, 
C      THUS AVOIDING FIRST ORDER ARTEFACT INHERENT TO DIRECT COMPUTATION 
C      OF THE HESSIAN IN THE CARTESIAN SPACE. 
C     -THE TRANSFORMATION MATRIX IS WORKED OUT BY ROUTINE 'JINCAR'. 
C     -INPUT AND OUTPUT HESSIAN IN THE SAME STORAGE H IN /OPTIM/. 
C     -DIAGONALIZE THE WEIGHTED FORCE CONSTANT ENERGY, 
C      AND PROVIDE VIBRATIONAL FREQUENCIES AND ZPE CONTRIBUTION. 
C      D.L. (MJS DEWAR GROUP) JUNE 1986 
C 
      COMMON /OPTIMI / IMP,IMP0                                         3GL3092
      COMMON /OPTMCR / H(MAXHES),FREQ(MAXPAR),                          3GL3092
     1                 T(MAXPAR,MAXPAR),COORD(3,NUMATM),                3GL3092
     2                 WTMASS(MAXPAR),                                  3GL3092
     3                 RDUMY(MAXPAR*(2*MAXPAR + NCHAIN + 14) +          3GL3092
     4                       29 + NCHAIN - 3*NUMATM)                    3GL3092
C     COMMON /OPTIM / IMP,IMP0,LEC,IPRT,H(MAXHES),FREQ(MAXPAR) 
C    .               ,T(MAXPAR,MAXPAR),COORD(3,NUMATM) 
C    .               ,WTMASS(MAXPAR) 
      COMMON /GEOKST/ NATOMS,LABELS(NUMATM),NA(NUMATM),NB(NUMATM) 
     .               ,NC(NUMATM) 
      COMMON /ATMASS/ ATMASS(NUMATM) 
      COMMON /GEOM  / GEO(3,NUMATM) 
C     COMMON /GEOVAR/ NVAR,LOC(2,MAXPAR) 
      COMMON /GEOVAR/ XPARAM(MAXPAR),NVAR,LOC(2,MAXPAR),IDUMY           3GL3092
      DIMENSION HT(MAXPAR,MAXPAR) 
      LOGICAL FAIL 
      EQUIVALENCE (HT(1,1),COORD(1,1)) 
      SAVE
C     CONVERSION IN MILLIDYNES/ANGSTR 'FACT'*0.5 
      DATA FACT /3.475625D-3/ 
C     AVOGADRO NUMBER 'AVOGA' 
      DATA AVOGA /6.022045D23/ 
C     PLANCK'S CONSTANT 'PLANCK' (JHZ) 
      DATA PLANCK /6.626176D-34/ 
C     SPEED OF LIGHT 'CSPEED' (CM/SEC) 
      DATA CSPEED /2.99792458D10/ 
C     CONVERSION FACTOR FOR SPEED OF LIGHT AND 2 PI 'C2PI' 
      C2PI=1.D0/(2.99792458D10*3.141592653598D0*2.D0) 
C     CONVERSION FACTOR FOR ZERO POINT ENERGY 
      FACT2=0.5D0*(AVOGA*PLANCK)*CSPEED/4.184D3 
C     ............... 
C     GET THE CARTESIAN (COORD) FROM THE INTERNAL (GEO),DUMMIES        INCLUDED 
      CALL INTCAR (GEO,COORD) 
      IF (ISTOP.NE.0) RETURN                                            GDH1095
C     BUILT THE JACOBIAN T(NVAR,NCART);T(I,J)=dINTERNAL(I)/dCARTESIAN(J) 
C     NVAR IS THE NUMBER OF INTERNAL VARIABLES, NCART=3*(NATOMS-DUMMY) 
      CALL JINCAR (COORD,T,NCART,FAIL) 
      IF (FAIL) THEN 
         WRITE(JOUT,150) 
         RETURN 
         ELSE IF(IMP.GT.4) THEN 
C        PRINTOUT THE JACOBIAN 
         WRITE(JOUT,130) 
         DO 10 I=1,NVAR 
   10    WRITE(JOUT,140) I,(T(I,J),J=1,NCART) 
      ENDIF 
C     BACK-TRANSFORM 
      DO 20 J=1,NCART 
   20 CALL SUPDOT(HT(1,J),H,T(1,J),NVAR,1) 
      K=0 
      DO 30 I=1,NCART 
      DO 30 J=1,I 
      K=K+1 
   30 H(K)=FACT*DOT(T(1,I),HT(1,J),NVAR) 
      IF(IMP.GT.3) THEN 
C        PRINTOUT THE FORCE MATRIX IN MILLIDYNES/ANGSTROM 
         WRITE(JOUT,100) 
         I=-NCART 
         CALL VECPRT(H,I) 
      ENDIF 
C     FIND CONVERSION CONSTANTS FOR MASS WEIGHTED SYSTEM 
      NUMAT=NCART/3 
      SQR2=SQRT(2.D0) 
      L=0 
      DO 40 I=1,NUMAT 
      WEIGHT=SQR2/SQRT(ATMASS(I)) 
      DO 40 J=1,3 
      L=L+1 
   40 WTMASS(L)=WEIGHT 
C     CONVERT 0.5*HESSIAN TO MASS WEIGHTED FORCE MATRIX 
      L=0 
      DO 50 I=1,NCART 
      IJ=I-NCART 
      DO 50 J=1,I 
      L=L+1 
      IJ=IJ+NCART 
   50 T(IJ,1)=H(L)*WTMASS(I)*WTMASS(J) 
C     DIAGONALIZE 
      CALL DIAGIV(T,NCART,NCART,FREQ,EPS1) 
C     SWITCH EIGENVALUES TO FREQUENCIES IN WAVE NUMBER 
      FACT3=SQRT(1.D5*AVOGA)*C2PI 
      EPS1=EPS1*FACT3 +1.D0 
      ZPEC=0.D0 
      MODE=0 
      DO 60 I=1,NCART 
      FREQ(I)=FACT3*SIGN(SQRT(ABS(FREQ(I))),FREQ(I)) 
      IF(FREQ(I).GT.EPS1) THEN 
         ZPEC=ZPEC+FREQ(I) 
         MODE=MODE+1 
      ENDIF 
   60 CONTINUE 
      WRITE(JOUT,110) (FREQ(I),I=1,NCART) 
      WRITE(JOUT,120)NVAR,MODE,ZPEC*FACT2 
C 
      RETURN 
  100 FORMAT(' FULL FORCE CONSTANT MATRIX IN CARTESIAN SPACE ', 
     .' (MILLIDYNES/ANGSTROM)') 
  110 FORMAT(' VIBRATIONAL FREQUENCIES (WAVE NUMBER)'/(10F8.1)) 
  120 FORMAT(' THE',I3,'-DIMENSIONAL HESSIAN EXHIBITS',I3,' VIBRATIONS', 
     .' CONTRIBUTING BY'/F10.2,' KCAL/MOLE TO THE ZERO POINT ENERGY') 
  130 FORMAT(/' JACOBIAN MATRIX dinternal(i)/dcartesian(j)'/ 
     .'      X1     Y1     Z1     X2     Y2     Z2     X3  ...') 
  140 FORMAT(I3,11F7.3/(3X,11F7.3)) 
  150 FORMAT(' *** AN OPTIMIZED INTERNAL COORDINATE USES A DUMMY ATOM ** 
     .*'/' ==> NO CALCULATIONS OF THE VIBRATIONAL FREQUENCIES ...'/ 
     .'     REMOVE THE DUMMIES ATOMS OR INVOKE ''FORCE''. SORRY') 
      END 
