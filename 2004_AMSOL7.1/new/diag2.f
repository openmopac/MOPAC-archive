
      SUBROUTINE DIAG2(FAO,VECTOR,NCLO,NOCC,EIG,N,FMO,WS)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)                               GCL0493
      INCLUDE 'SIZES.i'                                                 GCL0493
      EXTERNAL ISMAX
      DIMENSION FAO(*),VECTOR(N,*),EIG(*),FMO(NOCC,*),WS(*)
C ----------------------------------------------------------------------
C     "FAST" DIAGONALISATION PROCEDURE.
C INPUT FAO   : LOWER HALF TRIANGLE OF THE FOCK MATRIX OVER AOS.
C       VECTOR: OLD EIGENVECTORS, LEADING DIMENSION N.
C       NCLO  : NUMBER OF DOUBLY OCCUPIED MOLECULAR ORBITALS.
C       NOCC  : NUMBER OF OCCUPIED MOLECULAR ORBITALS.
C       EIG   : EIGENVALUES FROM AN EXACT DIAGONALISATION
C       N     : NUMBER OF ATOMIC ORBITALS IN BASIS SET
C  OUTPUT VECTOR: NEW EIGENVECTORS
C       WS AND FMO ARE WORK SPACE OF SIZE N*N EACH.
C  METHOD: CRUDE ROTATION OF MOS (CLOSED/OPEN), (CLOSED/VIRTUAL) AND
C          (OPEN/VIRTUAL) TO ROUGHLY BLOCK-DIAGONALISE THE FOCK MATRIX
C          OVER MOS.
C        - THE ROTATION REQUIRED TO ELIMINATE THOSE ELEMENTS CONSIDERED
C          SIGNIFICANT IS APPROXIMATED TO USING THE EIGENVALUES OF THE
C          EXACT DIAGONALISATION THROUGHOUT THE REST OF THE ITERATIVE
C          PROCEDURE.
C        - ALL THE APPROXIMATIONS PRESENT BECOME VALID AT CONVERGENCE.
C  REFERENCE: "FAST SEMIEMPIRICAL CALCULATIONS", STEWART J.J.P.,
C             CSASZAR P., PULAY P., J. COMP. CHEM., 3, 227,(1982)
C  TRIVIAL EXTENSION TO OPEN SHELLS AND UHF CASES, D.L. JUNE 90.
C ----------------------------------------------------------------------
C     SCALAR AND VECTORIZED VERSION
C     FIRST, CONSTRUCT FMO, THAT PART OF FOCK OVER MOS WHICH CONNECTS
C     (CLOSED+OPEN) AND (OPEN+VIRTUAL) SETS.
C     ----------------------------------------------------------------
      CALL UNCANO (FAO,N,FMO,N)
      LOPEN=NCLO+1
      NVIRT=N-NCLO
      CALL MXM (FMO,N,VECTOR(1,LOPEN),N,WS,NVIRT)
      CALL MTXM (VECTOR,NOCC,WS,N,FMO(1,LOPEN),NVIRT)
C     ERASE (IF ANY) THE OPEN/OPEN BLOC (NO ROTATION TO PERFORM).
CDIR$ NOVECTOR
      DO 20 I=LOPEN,NOCC
CDIR$ NOVECTOR
      DO 10 J=LOPEN,NOCC
   10 FMO(J,I)=0.D0
   20 CONTINUE
C     NOW DO A CRUDE 2 BY 2 ROTATION TO "ELIMINATE" SIGNIFICANT ELEMENTS
C     ------------------------------------------------------------------
      TINY=2D-2*ABS(FMO(NCLO*NOCC+ISMAX(NVIRT*NOCC,FMO(1,LOPEN),1),1))
      DO 40 I=LOPEN,N
      DO 30 J=1,NOCC
      C=FMO(J,I)
      IF (ABS(C).GT.TINY) THEN
C        DEFINE THE ANGLE OF ROTATION
         D=EIG(I)-EIG(J)
         E=MIN(1.D0,0.5D0*(1.D0+D/SQRT(4.D0*C**2+D**2)))
         ALPHA=SQRT(E)
         BETA=SIGN(SQRT(1.D0-E),C)
C        ROTATION OF PSEUDO-EIGENVECTORS
         CALL DROT(N,VECTOR(1,I),1,VECTOR(1,J),1,ALPHA,BETA)
      ENDIF
   30 CONTINUE
   40 CONTINUE
      RETURN
      END
