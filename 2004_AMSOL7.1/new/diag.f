      SUBROUTINE DIAG(FAO,VECTOR,NOCC,EIG,MDIM,N)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
       INCLUDE 'SIZES.i'
C***********************************************************************
C
C   "FAST" DIAGONALISATION PROCEDURE.
C
C    ON INPUT FAO CONTAINS THE LOWER HALF TRIANGLE OF THE MATRIX TO BE
C                         DIAGONALISED, PACKED.
C             VECTOR  CONTAINS THE OLD EIGENVECTORS ON INPUT, THE NEW
C             VECTORS ON EXITING.
C             NOCC = NUMBER OF OCCUPIED MOLECULAR ORBITALS.
C             EIG  = EIGENVALUES FROM AN EXACT DIAGONALISATION
C             MDIM = DECLARED SIZE OF MATRIX "VECTOR".
C                    (MUST EQUAL N IN THE CRAY VERSION)
C             N = NUMBER OF ATOMIC ORBITALS IN BASIS SET
C
C  DIAG IS A PSEUDO-DIAGONALISATION PROCEDURE, IN THAT THE VECTORS THAT
C       ARE GENERATED BY IT ARE MORE NEARLY ABLE TO BLOCK-DIAGONALISE
C       THE FOCK MATRIX OVER MOLECULAR ORBITALS THAN THE STARTING
C       VECTORS. IT MUST BE CONSIDERED PSEUDO FOR SEVERAL REASONS:
C       (A) IT DOES NOT GENERATE EIGENVECTORS - THE SECULAR DETERMINANT
C           IS NOT DIAGONALISED, ONLY THE OCCUPIED-VIRTUAL INTERSECTION.
C       (B) MANY SMALL ELEMENTS IN THE SEC.DET. ARE IGNORED AS BEING TOO
C           SMALL COMPARED WITH THE LARGEST ELEMENT.
C       (C) WHEN ELEMENTS ARE ELIMINATED BY ROTATION, THE REST OF THE
C           SEC. DET. IS ASSUMED NOT TO CHANGE, I.E. ELEMENTS CREATED
C           ARE IGNORED.
C       (D) THE ROTATION REQUIRED TO ELIMINATE THOSE ELEMENTS CONSIDERED
C           SIGNIFICANT IS APPROXIMATED TO USING THE EIGENVALUES OF THE
C           EXACT DIAGONALISATION THROUGHOUT THE REST OF THE ITERATIVE
C           PROCEDURE.
C
C  (NOTE:- IN AN ITERATIVE PROCEDURE ALL THE APPROXIMATIONS PRESENT IN
C          DIAG BECOME VALID AT SELF-CONSISTENCY, SELF-CONSISTENCY IS
C          NOT SLOWED DOWN BY USE OF THESE APPROXIMATIONS)
C
C    REFERENCE:
C             "FAST SEMIEMPIRICAL CALCULATIONS",
C             STEWART. J.J.P., CSASZAR, P., PULAY, P., J. COMP. CHEM.,
C             3, 227, (1982)
C
C***********************************************************************
      DIMENSION FAO(*),VECTOR(N,*),EIG(*)
C     COMMON /SCRACH/ FMO(MPACK/2),WS(MORB2),BUF(MORB2)
C
C     /SCRACH/ HAS BEEN CONVERTED TO /SCRCHR/ AND /SCRCHI/ FOR AMSOL
      COMMON /SCRCHR/ FMO(MPACK/2),WS(MORB2),BUF(MORB2),                3GL3092
     1                DUM3(6*MAXPAR**2+1-MPACK/2-2*MORB2)               GCL0393
C
C     FMO  IS A WORK-SPACE OF SIZE NOCC*(N-NOCC), IT WILL HOLD
C          THE FOCK MOLECULAR ORBITAL INTERACTION MATRIX.
C          ARRAY OF SIZE N*(N-NOCC).
C     WS AND BUF ARE WORK SPACE OF SIZE N*N (CRAY VERSION).
C
C     FIRST, CONSTRUCT FMO, THAT PART OF A SECULAR DETERMINANT OVER
C     MOLECULAR ORBITALS WHICH CONNECTS THE OCCUPIED AND VIRTUAL SETS.
C     ----------------------------------------------------------------
       SAVE
      K=0
      DO 10 I=1,N
      IJ=I
CDIR$ IVDEP
      DO 10 JI=N*(I-1)+1,N*(I-1)+I
      K=K+1
      BUF(JI)=FAO(K)
      BUF(IJ)=FAO(K)
   10 IJ=IJ+N
      LUMO=NOCC+1
      NVIRT=N-NOCC
      IF(NVIRT.EQ.0) RETURN                                             CC2-92
      CALL MXM (BUF,N,VECTOR(1,LUMO),N,WS,NVIRT)
      CALL MTXM(VECTOR,NOCC,WS,N,FMO,NVIRT)
      TINY=0.04D0*ABS(FMO(ISMAX(NVIRT*NOCC,FMO,1)))
C
C     NOW DO A CRUDE 2 BY 2 ROTATION TO "ELIMINATE" SIGNIFICANT ELEMENTS
C     ------------------------------------------------------------------
C
      IJ=0
      DO 120 I=LUMO,N
         EIGI=EIG(I)
         DO 110 J=1,NOCC
            IJ=IJ+1
            C=FMO(IJ)
            IF(ABS(C).LT.TINY) GOTO 110
C
C      BEGIN 2 X 2 ROTATIONS
C
            D=EIGI-EIG(J)
            E=MIN(1.D0,0.5D0*(1.D0+D/SQRT(4.D0*C*C+D*D)))
            ALPHA=SQRT(E)
            BETA=SIGN(SQRT(1.D0-E),C)
C
C      ROTATION OF PSEUDO-EIGENVECTORS
            DO 100 M=1,N
               A=VECTOR(M,J)
               B=VECTOR(M,I)
               VECTOR(M,J)=ALPHA*A-BETA*B
  100          VECTOR(M,I)=ALPHA*B+BETA*A
  110    CONTINUE
  120 CONTINUE
      RETURN
      END
