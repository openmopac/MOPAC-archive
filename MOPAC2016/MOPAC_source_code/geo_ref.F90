    subroutine geo_ref 
!
! geo_ref reads in the geometry reference file, and makes it as similar
! as possible to the current geometry.  It does this by:
!
! If the files contain PDB-type information, then putting the atoms of the
! reference geometry into the same sequence as in the current geometry.
!                                                                                                                                                    
! If any atoms cannot be mapped from reference to current geometry, then put those
! atoms at the end of the data sets.
!
! Rotate and translate the geometries to put them into maximum coincidence,
! i.e., minimize the RMS difference between the two geometries.
!
! During this process, swap pairs of atoms, if that will lower the RMS difference.
!
      USE vast_kind_param, ONLY:  double 
!
      use chanel_C, only : ir, iw, output_fn, job_fn
!
      use molkst_C, only : numat, keywrd, nvar, id, natoms, moperr, line, refkey, density, &
        maxtxt, numat_old, koment, title, geo_ref_name, geo_dat_name, arc_hof_2, arc_hof_1
!
      use common_arrays_C, only : geo, coord, xparam, na, nb, nc, nat, c, labels, txtatm, lopt, &
        txtatm1, geoa, atmass, loc
!
      use elemts_C, only: atom_names
      implicit none
! 
      integer :: i, j, k, l, ii, jj, i4, j4, k4, iquit, numat_dat, numat_ref
      integer, allocatable :: map_atoms_A(:), atom_no(:)
      character, allocatable :: tmp_txt(:)*26, diffs(:)*80
      double precision, allocatable :: tmp_atmass(:), tmp_geoa(:,:)
      real(double) :: dum1, dum2, sum, rms, rms_min, sum1, sum2, sum3, &
        toler, xmin, sum4
      double precision, external :: reada
      logical :: intern = .true., exists, bug, any_bug, swap, pdb, html, first, let
      logical, allocatable :: same(:), ok(:)
      character :: line_1*241, refkey_ref(6)*1000, num
!
!   For Geo-Ref to work, some very specific conditions must be satisfied.  
!   So before attempting a GEO_REF calculation, check that the data are okay
!
      do j = 2, natoms
        if (na(j) /= 0) exit
      end do       
      if (nvar /= 3*numat + 3*id) then
        write(iw,"(a)")" GEO_REF requires all parameters to be optimized"
        write(iw,"(a)")" (The easiest way to do this is to add ""OPT"" to the set of keywords.)"
        call mopend("GEO_REF requires all parameters to be optimized")
      else if (j <= natoms) then
        write(iw,"(a)")" GEO_REF requires Cartesian coordinates"
        write(iw,"(a)")" (The easiest way to do this is to add ""XYZ"" to the set of keywords.)"
        call mopend("GEO_REF requires Cartesian coordinates")
      end if       
      if (moperr) return
      allocate(geoa(3,natoms + 300), c(3,natoms + 300)) ! Generous safety factor for second geometry
      do ii = 1, 6
        line = " "//trim(refkey(ii))
        call upcase(line, len_trim(line))
        i = index(line," GEO_REF")
        if (i /= 0) exit
      end do
      j = index(refkey(ii)(i + 10:),' ') + i + 8
      if (index(line(i:j), '"')  + index(line(i:j), "'") == 0) then
        write(line,'(a)')" File name after GEO_REF must be in quotation marks."
        call mopend(trim(line))
        return
      end if
      j = index(refkey(ii)(i + 10:),'"') +  index(refkey(ii)(i + 10:), "'")
      if (j == 0) then
        write(line,'(a)')" File name after GEO_REF must end with a quotation mark."
        call mopend(trim(line))
        return
      end if
      j = j + i + 8
      line = refkey(ii)(i + 9:j)
      line_1 = trim(line)
      call upcase(line_1, len_trim(line_1))
      if (line_1 == "SELF") then
        line = output_fn(:len_trim(output_fn) - 3)//"mop"
        refkey(ii)(i:) = "geo_ref="""//trim(line)//refkey(ii)(j + 1:)
      end if
      geo_ref_name = trim(line)
      k = index(keywrd," GEO_DAT") + 10
      if (k > 10) then
        do ii = 1, 6
          line = " "//trim(refkey(ii))
          call upcase(line, len_trim(line))
          i = index(line," GEO_DAT")
          if (i /= 0) exit
        end do
        j = index(refkey(ii)(i + 9:),'" ') + i + 7
        geo_dat_name = refkey(ii)(i + 9:j)
      else
        geo_dat_name = trim(job_fn)
      end if 
      if (index(keywrd," 0SCF") /= 0 .and. index(keywrd, " HTML ") /= 0) then
        line = trim(geo_dat_name)
        call upcase(line, len_trim(line))
        line = line(len_trim(line) - 3:)
        if (line == ".PDB") &
          call mopend("GEO_DAT file cannot be a PDB file (It would be over-written)")
        line = trim(geo_ref_name)
        call upcase(line, len_trim(line))
        line = line(len_trim(line) - 3:)
        if (line == ".PDB") &
          call mopend("GEO_REF file cannot be a PDB file (It would be over-written)")
        if (moperr) return
      end if    
      line = trim(geo_ref_name)
      inquire (file=trim(line), exist = exists)
      if (.not. exists) then
        call add_path(line)
        inquire (file=trim(line), exist = exists)
        if ( .not. exists) then
          inquire (file=trim(line)//".mop", exist = exists)
          if (exists) line = trim(line)//".mop"
        end if
        if (.not. exists) then
          call mopend ("GEO_REF file '"//trim(line)//"' does not exist.")
          return
        end if
      end if
      open(unit = 99, file = trim(line), iostat = i)
      if (i /= 0) then
        call mopend ("Problem opening file "//trim(line))
        return
      end if
      nat(:natoms) = labels(:natoms)
      i = natoms 
      rewind 99
      do i = 1, 100000
        read(99,"(a)", iostat = j) num
        if (j /= 0) exit
      end do
      rewind 99
      allocate(map_atoms_A(numat), tmp_txt(numat),  tmp_atmass(numat), atom_no(numat))
      tmp_txt(:numat) = txtatm(:numat)
      tmp_atmass(:numat) = atmass(:numat)
      if (i > numat) then
        deallocate (txtatm, txtatm1, labels, atmass)
        deallocate (geoa, c, coord, lopt, na, nb, nc)
        allocate(txtatm(i), txtatm1(i), labels(i), atmass(i))
        allocate(geoa(3,i), c(3,i), coord(3,i),lopt(3,i), na(i), nb(i), nc(i))  
      end if
      call upcase(line, len_trim(line))
      if (index(line, '.ARC') /= 0) then
        do
          read(99,"(a)", iostat=ii)line_1
          if (ii /= 0) exit
          if (index(line_1, "HEAT OF FORMATION") > 0) arc_hof_2 = reada(line_1,20)
          if (index(line_1," FINAL GEOMETRY OBTAINED") /= 0) exit
        end do
        if (ii /= 0) rewind(99)
      end if
      i = 0
      do
        i = i + 1
        read(99,"(a)", err=99)refkey_ref(i)  !  Dummy read over first three lines
        if (refkey_ref(i)(1:1) == "*") i = i - 1
        if (i == 1) then
          if (index(refkey_ref(i), " +") /= 0) i = i - 1
        end if
        if (i == 3) exit
      end do
      refkey_ref(4:6) = "    NULL"
      goto 97
99    write(iw,*)" File' "//trim(line)//"' is faulty"
      return
97    i = index(keywrd," GEO_REF") + 11
      do
        if (keywrd(i:i) == '"' .or. keywrd(i:i) == "'") exit
        i = i + 1
      end do
      if (keywrd(i + 1: i + 1) == " ") then
        if (index(keywrd," 0SCF") == 0 .or. index(keywrd, " HTML ") == 0) &
        write(iw,'(/10x,a)')"By default, a restraining force of 3 kcal/mol/A^2 will be used"
        density = 3.d0
      else
        density = reada(keywrd, j + 2)
        write(iw,'(/10x,a,f8.3,a)')"A restraining force of",density," kcal/mol/A^2 will be used"
      end if
      if (id == 3 .and. abs(density) > 1.d-10) then
        write(line,'(a)')"For solids, the restraining force in GEO_REF must be 0.0."
        write(iw,'(//10x,a,/)')trim(line)
        call mopend(trim(line))
        return
      end if
      numat_dat = numat
!
!  Put atoms in the reference data set into the same order as those in the current data set
!
      ii = numat
      j = maxtxt
      call getgeo (99, labels, geoa, c, lopt, na, nb, nc, intern) 
      deallocate(c)
      maxtxt = j
      if (moperr .and. natoms == 0) then
        call mopend("An error was detected in the reference data set.")
        return
      end if
      if (natoms == -2) then
        j = ir
        ir = 99
        rewind(99)        
        call getpdb(geoa)
        ir = j
        numat_old = numat
        pdb = .true.
      else
        pdb = .false.
      end if
      txtatm1(:ii) = tmp_txt(:ii)
      numat = numat - id    
      i = max(ii, numat)
      numat_ref = numat
      allocate (tmp_geoa(3,i), same(i), ok(i), diffs(i))
      diffs = " "
      same = .false.
      ok = .false.
      k = 0
      l = 0
      if (index(keywrd," 0SCF") /= 0 .and. index(keywrd, " HTML ") /= 0) then
!
! Put atoms in the GEO_REF file into the same order as those in the data-set
!
!    GEO_REF arrays:   geoa, txtatm, labels
!    dataset arrays:   geo, txtatm1, nat
!
!  Align atoms in three passes.  In the first pass, all atoms with the same label are aligned.
!  In the second pass, hydrogen atoms that might be mis-labeled within a residue are assigned.
!  In the third pass, any remaining hydrogen atoms are assigned.
!
        do j = 1, ii
          j4 = 0
!
!  First pass
!
          do i = 1, numat
            if (same(i)) cycle
            if (txtatm1(j)(12:) == txtatm(i)(12:)) then
              k = k + 1
              tmp_txt(k) = txtatm(i)
              coord(:,k) = geo(:,j)
              tmp_geoa(:,k) = geoa(:,i)   
              labels(k) = nat(j)
              same(i) = .true.
              ok(j) = .true.
              j4 = 1
              exit
            end if
          end do
        end do
        let = (index(keywrd," LET") /= 0) 
        do j = 1, ii
!
!  Second pass
!
          if(ok(j)) cycle
          do i4 = 1, numat
            if (same(i4)) cycle
            if (txtatm1(j)(14:14) == "H" .and. txtatm(i4)(14:14) == "H" .and. &
              txtatm1(j)(18:) == txtatm(i4)(18:)) then
              k = k + 1
              tmp_txt(k) = txtatm(i4)
              coord(:,k) = geo(:,j)
              tmp_geoa(:,k) = geoa(:,i4)   
              labels(k) = nat(j)
              same(i4) = .true.
              ok(j) = .true.
              exit
            end if
          end do
          if (.not. ok(j) .and. .not. let) then
            l = l + 1
            write(diffs(l)(:40),'(3x,a)')txtatm1(j)
          end if 
        end do 
        if (let) then
          first = .true.
          do j = 1, ii
!
!  Third pass
!
            if(ok(j)) cycle
            do i4 = 1, numat
              if (same(i4)) cycle
              if (txtatm1(j)(14:14) == "H" .and. txtatm(i4)(14:14) == "H") then
                if (first) then
                  write(iw,'(/16x,a)')"Hydrogen atoms that are on different residues"
                  if (trim(job_fn) == trim(geo_dat_name)) then
                    line = "dataset"
                  else
                    line = "GEO_DAT"
                  end if
                  write(iw,'(/6x,a,14x,a,/)')"Atoms in "//trim(line)//" only","     Atoms in GEO_REF only"
                  first = .false.
                end if      
                line = txtatm1(j)
                line(41:) = txtatm(i4)
                write(iw,'(4x,a)')trim(line)              
                k = k + 1
                tmp_txt(k) = txtatm(i4)
                coord(:,k) = geo(:,j)
                tmp_geoa(:,k) = geoa(:,i4)   
                labels(k) = nat(j)
                same(i4) = .true.
                ok(j) = .true.
                exit
              end if
            end do
            if (.not. ok(j)) then
              l = l + 1
              write(diffs(l)(:40),'(3x,a)')txtatm1(j)
            end if 
          end do 
          if (.not. first) write(iw,'(/18x,a)')"(Atom labels from GEO_REF will be used)"
        end if
        geo(:,:k) = coord(:,:k)
        ii = k
        jj = 0
        do i = 1, numat
          if (.not. same(i)) then
            jj = jj + 1
            write(diffs(jj)(41:),'(3x,a)')txtatm(i)
          end if
        end do
        nat(:k) = labels(:k)
        txtatm(:k) = tmp_txt(:k)
        l = max(l, jj)
        if (l > 0) then
          write(iw,'(/28x,a)')"Differences in atoms sets"
          write(iw,'(/10x,a,14x,a,/)')"Atoms in data-set only","    Atoms in GEO_REF only"
          do i = 1, l
            write(iw,'(4x,a)')trim(diffs(i))
          end do
        end if
        geoa(:,:k) = tmp_geoa(:,:k)
        tmp_txt(:k) = txtatm(:k)
        if (numat_dat /= numat_ref) then
          if (index(keywrd, " GEO-OK") == 0) then  
            write(line,'(a, i5, a, i5)')"Number of atoms in data-set:",numat_dat, ", in GEO_REF:", numat_ref
            call mopend(trim(line))
            i = index(keywrd," GEO_DAT") + 9
            if (i > 9) then
              write(iw,'(/10x, a,/10x,a, /)')"Docking can only be done when the number of atoms in ", &
              "the files defined by GEO_DAT and GEO_REF are the same."
            else
             write(iw,'(/5x, a, /)')"Docking can only be done when the number of atoms in the data-set and GEO_REF are the same."
            end if
            i = (k*100)/max(numat,ii)
            write(iw,'(10x,a,i3,a)')"(The two systems have",i, &
            "% of atoms in common. To continue, but using","only those atoms that are common to both systems, add ""GEO-OK"")"       
            i = index(keywrd," GEO_DAT") + 9
            if (i > 9) then
              write(iw,'(/10x,a)')"GEO_DAT name: """//trim(geo_dat_name)//""""
            else
              write(iw,'(/10x,a)')"Data-set name: "//trim(geo_dat_name)
            end if           
            write(iw,'(/10x,a)')"GEO_REF name: """//trim(geo_ref_name)//""""
            return
          end if
          write(iw,'(/22x,a,i5)')"Number of atoms in dataset:",numat_dat
          write(iw,'(22x,a,i5)')   "Number of atoms in GEO_REF:",numat_ref
          write(iw,'(10x,a,i5,/)') "Number of atoms common to both systems:",ii
        end if      
        natoms = k + id
        numat = k
        if (maxtxt == 26) txtatm1(:numat) = txtatm(:numat)
        txtatm(:numat) = tmp_txt(:numat)
        atmass(:numat) =  tmp_atmass(:numat)
        coord(:,:numat) = geoa(:,:numat)
      end if
      if (moperr) then
        i = index(keywrd," GEO_REF")
        j = index(keywrd(i + 10:),'"') + i + 8
        line = keywrd(i + 10:j)
        write(line,'(a)')"Fault detected in GEO_REF data set: '"//trim(line)//"'"
        write(iw,'(a)')trim(line)
        call mopend(trim(line))
        return
      end if
      do j = 2, natoms
        if (na(j) > 0) exit
      end do
      if (j <= natoms) then
        call gmetry(geoa, coord)
        geoa(:,:natoms) = coord
        na(:natoms) = 0
      end if    
      if (maxtxt == 26 .and. txtatm(1)(:4) == "ATOM" .and. txtatm1(1)(:4) == "ATOM" &
        .and. index(keywrd,"GEO-OK") == 0 .and. index(keywrd,"""SELF""") == 0) then
!
!  Put atoms in the reference data set into the same order as those in the current data set
!
        bug = .false.
        any_bug = .false.
!
!  Check for faults in the data-set labels
!
        call compare_txtatm(bug, any_bug)
        bug = .false.
!
!  Check for faults in the geo_ref labels
!
        do i = 1, numat
          if (labels(i) == 1) then
            l = 1
            do j = i + 1, numat
              if (labels(j) == 1) then
                if (txtatm(i)(12:) == txtatm(j)(12:)) then
                  do k = 13, 16
                    if (txtatm(j)(k:k) == " ") then
                      l = l + 1
                      txtatm(j)(k:k) = char(l + ichar("0"))
                      txtatm1(j)(k:k) = char(l + ichar("0"))
                      exit
                    end if
                  end do
                end if
              end if
              if (l == 9) exit
            end do
            if (l > 1) then
              do k = 13, 16
                if (txtatm(i)(k:k) == " ") then
                  txtatm(i)(k:k) = "1"
                  txtatm1(i)(k:k) = "1"
                  exit
                end if
              end do
            end if                
          end if
        end do
        k = 0
        do i = 1, numat
          do j = i + 1, numat
            if (txtatm1(i)(12:) == txtatm1(j)(12:)) exit
          end do
          if (j <= numat) then
            if ( .not. bug) then
              ii = index(keywrd," GEO_REF")
              jj = index(keywrd(ii + 10:),'"') + ii + 8
              line = keywrd(ii + 10:jj)
              write(iw,'(/10x,a,/)')"Atoms in the GEO_REF file '"// &
              trim(line)//"' with the same labels"
            end if
            write(iw,'(10x,a,i6,a,i6,a)')"Atoms", i, " and", j, &
              ";  Labels: ("//txtatm1(i)//") and ("//txtatm1(j)//")"
              bug = .true.
              k = k + 1
          end if
        end do
        if (bug) then
          write(iw,'(/10x,a,/)')"Edit GEO_REF data set and re-run."
          any_bug = .true.
          bug = .false.            
        end if
!
!  Check for labels in the data-set that are not in geo_ref
! 
        do i = 1, numat
          do j = 1, numat
            if (txtatm(i)(12:) == txtatm1(j)(12:)) exit
          end do
          if (j > numat) then
            if ( .not. bug) then
              ii = index(keywrd, "GEO_DAT=")
              if (ii > 0) then

                write(iw,'(/10x,a,/)')"Atoms in the GEO_DAT file '"// &
                trim(geo_dat_name)//"' with no equivalent in the geo_ref file"
              else
                write(iw,'(/10x,a,/)')"Atoms in the data-set file '"// &
                trim(geo_dat_name)//"' with no equivalent in the geo_ref file"
              end if
            end if
            write(iw,'(10x,a,i6,a,i6,a)')"Atom", i, ";  Label: ("//txtatm(i)//")"
              bug = .true.
              any_bug = .true.
              k = k + 1
          end if
        end do
        bug = .false.          
        do i = 1, numat
          do j = 1, numat
            if (txtatm1(i)(12:) == txtatm(j)(12:)) exit
          end do            
          if (j > numat) then
            if ( .not. bug) then
              write(iw,'(/10x,a,/)')"Atoms in the GEO_REF file '"// &
                & trim(geo_ref_name)//"' with no equivalent in the data-set file"
            end if
            write(iw,'(10x,a,i6,a,i6,a)')"Atom", i, ";  Label: ("//txtatm1(i)//")"
              bug = .true.
              any_bug = .true.
              k = k + 1
          end if
        end do
        if (any_bug) then
          if (k == 1) then
            call mopend("Fault detected in atom labels in a GEO_REF run.")
          else
            call mopend("Faults detected in atom labels in a GEO_REF run.")
          end if
          call mopend("(To continue with the current data set, use 'GEO-OK')")
          call web_message(iw,"geo_ref.html")
          return
        end if
        map_atoms_A = -1
        do i = 1, numat
          do j = 1, numat
            if (txtatm(i)(12:) == txtatm1(j)(12:)) exit
          end do
          if (j <= numat) then
            map_atoms_A(j) = i
          end if 
        end do  
        do i = 1, numat
          if (map_atoms_A(i) == -1) then
            do k = 1, numat
              do j = 1, numat
                if (map_atoms_A(j) == k) exit
              end do
              if (j > numat) then
                map_atoms_A(i) = k
                exit
              end if
            end do
          end if 
        end do             
        do i = 1, numat
          do j = 1, numat
            if (txtatm(i)(12:) == txtatm1(j)(12:)) exit
          end do
          if (j <= numat) then
            map_atoms_A(j) = i
          end if 
        end do        
!
!  At this point, the data set geometry is used in defining the atom order.
!  map_atoms_A(i) 
!  
        tmp_txt(:numat) = txtatm1(:numat)
        do i = 1, numat
          j = map_atoms_A(i)
          geoa(:,j) = coord(:,i)
          txtatm1(j) = tmp_txt(i)
          atom_no(j) = i
        end do 
        else
          do i = 1, numat
            atom_no(i) = i
          end do
      end if
      labels(:numat) = nat(:numat)
      coord(:,:numat) = geoa(:,:numat)
      rms_min = 1.d6
      if (.not. allocated(same)) allocate(same(numat))
      call geo_diff(sum, rms, .false.)              
      toler = 3.d0        
      i4 = 1
      j4 = numat 
      k4  = 1      
      iquit = 0
      xmin = 1.d8  
      exists = .true.
      swap = (index(keywrd, " NOSWAP") == 0)
      do ii = 1,30
        call dock (geoa, geo, sum)     
        sum = 0.d0
        do i = 1, numat
          j = j + 1
          sum = sum + sqrt((geo(1,i) - geoa(1,i))**2 + &
                            (geo(2,i) - geoa(2,i))**2 + &
                            (geo(3,i) - geoa(3,i))**2)
        end do 
        if (swap .and. ii > 1) then
!
!  Check that atoms are in maximum coincidence.
!  If they are not, then re-arrange geoa
!       
          do i = 1, numat            
            sum = (geo(1,i) - geoa(1,i))**2 + &
                  (geo(2,i) - geoa(2,i))**2 + &
                  (geo(3,i) - geoa(3,i))**2
            same(i) = (sum < toler)
          end do
          jj = 0
          do i = i4, j4, k4          
            sum = (geo(1,i) - geoa(1,i))**2 + &
                  (geo(2,i) - geoa(2,i))**2 + &
                  (geo(3,i) - geoa(3,i))**2
            if (sum > toler) then
              sum = 1.d8
              k = i
              do j = i4, j4, k4
                if (txtatm1(i)(:6) == txtatm1(j)(:6)     .and.  &
                  txtatm1(i)(22:)   == txtatm1(j)(22:)   .and.  &
                  txtatm1(i)(14:14) == txtatm1(j)(14:14) .and.  &
                  labels(i) == labels(j) .and. .not. (same(i) .and. same(j))) then
                  num = txtatm1(i)(15:15)
                  if (num == " " .or. (num >= "0" .and. num <= "9")) then
                    num = txtatm1(j)(15:15)
                    if (num == " " .or. (num >= "0" .and. num <= "9")) num = "+"
                  end if
                  if (num /= "+") cycle                   
                  sum2 = ((geo(1,j) - geoa(1,j))**2 + &
                          (geo(2,j) - geoa(2,j))**2 + &
                          (geo(3,j) - geoa(3,j))**2)
                  sum3 = ((geo(1,i) - geoa(1,i))**2 + &
                          (geo(2,i) - geoa(2,i))**2 + &
                          (geo(3,i) - geoa(3,i))**2) 
                  dum1 = ((geo(1,i) - geoa(1,j))**2 + &
                          (geo(2,i) - geoa(2,j))**2 + &
                          (geo(3,i) - geoa(3,j))**2) 
                  dum2 = ((geoa(1,i) - geo(1,j))**2 + &
                          (geoa(2,i) - geo(2,j))**2 + &
                          (geoa(3,i) - geo(3,j))**2)
!
!  If swapping the atoms around will improve the overlap, then do so.
!
!  dum1 = distance contribution from atom i if atom i and j were swapped around.
!  dum2 = distance contribution from atom j if atom i and j were swapped around.
!  sum2 = distance contribution from atom j if the atoms were not swapped around
!  sum3 = distance contribution from atom i if the atoms were not swapped around
!  sum  = smallest distance contribution, i.e., best choice for swapping
!
                  if (dum1 + dum2 < sum - 0.1d0 .and. dum1 + dum2 < sum2 + sum3 - 0.1d0) then
                    sum = dum1 + dum2
                    sum4 = sum2 + sum3
                    k = j
                  end if
                end if
              end do
              if (i /= k) then
                jj = jj + 1
                if (exists) then
                  exists = .false.
                  write(iw,'(" ",20("----"))')
                  if (maxtxt == 26) then
                    write(iw,'(/4x,a,//8x,a,/)')"  List of atoms in GEO_REF that are swapped in order to maximize overlap", &
                    "      Atom Label          and         Atom Label         Difference to GEO_REF"            
                  else
                    write(iw,'(/4x,a,//8x,a,/)')"  List of atoms that are swapped in order to maximize overlap", &
                    "  Atom name   Atom No. and Atom No.  Difference to GEO_REF"       
                  end if
                end if
                if (maxtxt == 26) then
                  write(iw,'(i5, a, 2x, a, f12.2)')jj,  " ("//txtatm1(i)//")",  &
                  " ("//txtatm1(k)//")", sqrt(sum) - sqrt(sum4)
                else
                    write(iw,'(i5,1x, a, i9,  i13, 1x, f12.2)')jj, atom_names(nat(i)), atom_no(i),  atom_no(k), &
                  sqrt(sum) - sqrt(sum4)
                end if
                do j = 1,3
                  sum = geoa(j,i)
                  geoa(j,i) = geoa(j,k)
                  geoa(j,k) = sum
                end do    
                line_1 = txtatm1(i)
                txtatm1(i) = txtatm1(k)
                txtatm1(k) = trim(line_1)
                j = atom_no(i)
                atom_no(i) = atom_no(k)
                atom_no(k) = j
              end if
              same(i) = .true.
            end if                
          end do
        end if
        if (.not. exists) write(iw,*)" "
        k = 0
        do i = 1, numat
          do l = 1,3
            k = k + 1
            xparam(k) = geo(l,i)
          end do
        end do
          call geo_diff(sum, rms, .false.)         
          sum1 = sum/numat       
          sum2 = sqrt(rms/numat)    
        if (abs(rms_min - rms) < 1.d-4 .and. ii > 2) exit
        if (xmin > rms) then
          xmin = rms
          iquit = 0
        else
          iquit = iquit + 1
          if (iquit > 3) exit
        end if
        rms_min = rms
        toler = max(1.d0, 0.7d0*toler)   
        k4 = i4
        i4 = j4
        j4 = k4
        if (i4 == 1) then 
          k4 = 1
        else
          k4 = -1
        end if
      end do
      txtatm1(:numat) = txtatm(:numat)
      if (sum1 < 1.d-10 .and. index(keywrd, " LOCATE-TS") /= 0) then
        write(line,'(a)')" The data-set and GEO_REF geometries are the same.  Correct the fault and re-run."
        call mopend (trim(line))
        return
      end if
      if (index(keywrd,"""SELF""") == 0) write(iw,'(/25x,a)')"After docking"
      call geo_diff(sum, rms, .true.)  
      if (trim(job_fn) == trim(geo_dat_name)) then
        line = "dataset"
      else
        line = "GEO_DAT"
      end if
      if (index(keywrd,"""SELF""") == 0) write(iw,'(/3x,a,f8.2,a,f8.4,a,f8.4,a)') &
        "Difference between "//trim(line)//" and GEO_REF:", sum1*numat, &
        " = total,", sum1, " = Average,", sum2," = RMS, in Angstroms"      
      close(99)
      if (index(keywrd," 0SCF") /= 0 .and. index(keywrd, " HTML ") /= 0) then
!
! Write out the reference geometry file needed for comparing the two geometries using JSmol.
!
        line = geo_ref_name(:len_trim(geo_ref_name) - 3)//"pdb"
        call add_path(line)
        open(unit = 99, file = trim(line), iostat = i)
        call l_control("HTML", len_trim("HTML"), -1)  
        coord(:,:numat) = geoa(:,:numat)
        call pdbout(99)
        call l_control("HTML", len_trim("HTML"), 1)  
        close (99)
      end if
      coord(:,:natoms) = geo(:,:natoms)   ! Store input geometry
      do i = 1, nvar
        xparam(i) = geoa(loc(2,i), loc(1,i))
      end do
      geo(:,:numat) = geoa(:,:numat)
!
! The following "big_swap" saves the geometry and xparam for the rotated input geometry
!
      call big_swap(0,2)
      geo(:,:natoms) = coord(:,:natoms)   ! Restore input geometry        
      do i = 1, nvar
        xparam(i) = geo(loc(2,i), loc(1,i))
      end do
      if (index(keywrd," 0SCF") /= 0) then
        call wrt_diffs()
        if (abs(arc_hof_1) + abs(arc_hof_2) > 1.d-4) write(iw,*)
        if (trim(job_fn) == trim(geo_dat_name)) then
          line = "dataset"
        else
          line = "GEO_DAT"
        end if
        if (abs(arc_hof_1) > 1.d-4) &
          write(iw,"(10x,a,f12.3,a)")  "Heat of formation of "//trim(line)//" system:",arc_hof_1," Kcal/mol"
        if (abs(arc_hof_2) > 1.d-4) &
          write(iw,"(10x,a,f12.3,a)")  "Heat of formation of GEO_REF system:",arc_hof_2," Kcal/mol"
        if (abs(arc_hof_1) > 1.d-4 .and. abs(arc_hof_2) > 1.d-4) &
          write(iw,"(40x,a,f12.3,a)")  "Diff.:", arc_hof_1 - arc_hof_2," Kcal/mol"
        call analyze_h_bonds()
      end if
      do ii = 1, 6
        line = " "//trim(refkey(ii))
        call upcase(line, len_trim(line))
        i = index(line," GEO_REF")
        if (i /= 0) exit
      end do
      if (index(keywrd,"""SELF""") == 0) then
        if (index(keywrd," 0SCF") == 0 .or. index(keywrd, " HTML ") == 0) then
          line = geo_dat_name(:len_trim(geo_dat_name) - 3)//"new"
          call add_path(line)
          open(unit = 99, file = trim(line), iostat = i)
        end if
      end if
      if (index(keywrd, " TS ") /= 0) then
        write(iw,'(/,a)')"    The average of the supplied and reference geometry will be written to:"
        write(iw,'(a)')"'"//trim(line)//"'"
        geo(:,:natoms) = 0.5d0*(geoa(:,:natoms) + geo(:,:natoms))
        call geout(99)
      else if (index(keywrd,"""SELF""") == 0) then 
        if (index(keywrd," 0SCF") == 0 .or. index(keywrd, " HTML ") == 0) then
          if (index(keywrd, " SWAP ") /= 0) then
            write(iw,'(/4x,a)')"The rotated-translated-re-arranged input geometry will be written to: '"//trim(line)//"'"
          else
            write(iw,'(/,a)')"    The rotated-translated input geometry will be written to: '"//trim(line)//"'"
          end if
        else
          do i = len_trim(output_fn), 1, -1
            if (output_fn(i:i) == "/" .or. output_fn(i:i) == "\") exit
          end do
          if (i > 0) then
            write(iw,'(//,a)')"    The following files will be written to """//output_fn(:i)//""":"
          else
            write(iw,'(//,a)')"    The following files will be written:"
          end if
          line = job_fn(i + 1:len_trim(job_fn) - 3)//"html"
          write(iw,'(a)')"    '"//trim(line)//"'"  
          line = geo_dat_name(:len_trim(geo_dat_name) - 3)//"pdb"
          write(iw,'(a)')"    '"//trim(line)//"'"
          line = geo_ref_name(:len_trim(geo_ref_name) - 3)//"pdb"
          write(iw,'(a)')"    '"//trim(line)//"'"         
        end if
        line = refkey_ref(1)
        do i = 1,3
          line = refkey(i)
          refkey(i) = refkey_ref(i)
          refkey_ref(i) = line
        end do           
        refkey_ref(4) = koment
        refkey_ref(5) = title
        if (index(keywrd," 0SCF") /= 0 .and. index(keywrd, " HTML ") /= 0) then
!
! Write out the input geometry file needed for comparing the two geometries using JSmol.
!
            close (99)
            if (sum2 > 9.9995d0) then
              num = "6"
            else
              num = "5"
            end if
            write(line,'(a,f'//num//'.3,a)')"RMS_DIFF=",sum2
            call l_control(trim(line), len_trim(line), 1) 
            line = geo_dat_name(:len_trim(geo_dat_name) - 3)//"pdb"
            coord(:,:numat) = geo(:,:numat)
            call add_path(line)            
            open(unit = 99, file = trim(line), iostat = i)  
            call pdbout(99)
        else
          koment = " NULL"
          title = " NULL"
          if (pdb) then
            coord(:,:natoms) = geoa(:,:natoms)  
            html = (index(keywrd, " HTML") /= 0) 
            if (html) call l_control("HTML", len_trim("HTML"), -1)   
            call pdbout(99)
            if (html) call l_control("HTML", len_trim("HTML"), 1)  
            coord(:,:numat) = geo(:,:numat)   ! Then restore the input geometry
          else
            geo(:,:natoms) = geoa(:,:natoms)  ! Temporarily, put the reference geometry into geo
            call geout(99)                    ! Write out the re-organized reference data set.    
            geo(:,:numat) = coord(:,:numat)   ! Then restore the input geometry
          end if    
        end if
        do i = 1,3
          line = refkey_ref(i)
          refkey_ref(i) = refkey(i)
          refkey(i) = line
        end do
        koment = trim(refkey_ref(4))
        title = trim(refkey_ref(5))
        close(99)
      end if
      deallocate(same)
      if (index(keywrd," 0SCF") + index(keywrd, " TS ") /= 0) then
        call mopend("GEO_REF with 0SCF: Job complete")
        return
      end if
      geo(:,:natoms) = coord(:,:natoms)
      return
  end subroutine geo_ref
  subroutine compare_txtatm(bug, any_bug)
    use molkst_C, only : numat, maxtxt, keywrd, line
    use common_arrays_C, only : txtatm, txtatm1, nat
    use chanel_C, only : iw, job_fn
    implicit none
    logical, intent (inout) :: bug, any_bug
    integer :: i, j, ii,jj
    if (index(keywrd, "GEO-OK") /= 0) return
    if (index(keywrd, "GEO_REF") == 0) return
    bug = .false.
    if (maxtxt /= 26) return
    do i = 1, numat
      do j = 1, 2
        if (txtatm(i)(20:20) /= " ") exit
        txtatm(i)(18:20) = " "//txtatm(i)(18:19)
      end do
      do j = 1, 2
        if (txtatm1(i)(20:20) /= " ") exit
        txtatm1(i)(18:20) = " "//txtatm1(i)(18:19)
      end do      
    end do
    do i = 1, numat
      do j = i + 1, numat
        if (txtatm(i)(12:) == txtatm(j)(12:)) exit
      end do
      if (j <= numat .and. nat(i) /= 1) then
        if ( .not. bug) then
          ii = index(keywrd, "GEO_DAT=")
         if (ii > 0) then
            jj = index(keywrd(ii + 9:),'"') + ii + 7
            line = keywrd(ii + 9:jj)
            write(iw,'(/10x,a,/)')"Atoms in the GEO_DAT file '"// &
            trim(line)//"' with the same labels"
          else
            write(iw,'(/10x,a,/)')"Atoms in the data-set file '"// &
            trim(job_fn)//"' with the same labels"
          end if
          write(iw,'(10x,a,i6,a,i6,a)')"Atoms", i, " and", j, &
          ";  Labels: ("//txtatm(i)//") and ("//txtatm(j)//")"
        end if
        bug = .true.
      end if
    end do
    if (bug) then
      call mopend("Error in data detected while using GEO_REF")
      write(iw,"(5x,a)")"(To continue with the current data set, use 'GEO-OK')"
      any_bug = .true.         
    end if
    return
  end subroutine compare_txtatm
